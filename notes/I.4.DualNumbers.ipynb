{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# I.4 Dual Numbers\n",
    "\n",
    "Int his chapter we introduce a mathematically beautiful  alternative to divided differences for computing\n",
    "derivatives: _dual numbers_. As we shall see, these are a commutative ring that _exactly_ compute derivatives,\n",
    "which when implemented in floating point give very high-accuracy approximations to derivatives. They\n",
    "underpin forward-mode [automatic differentation](https://en.wikipedia.org/wiki/Automatic_differentiation).\n",
    "\n",
    "\n",
    "Before we begin, we must be clear what a \"function\" is. Consider three possible scenarios:\n",
    "\n",
    "1. _Black-box function_: this was considered last chapter where we can only input floating-point numbers\n",
    "and get out a floating-point number.\n",
    "2. _Generic function_: Consider a function that is a formula (or, equivalentally, a _piece of code_) \n",
    "that we can evaluate  on arbitrary types, including\n",
    "custom types that we create. An example is a polynomial:\n",
    "$$\n",
    "p(x) = p_0 + p_1 x + â‹¯ + p_n x^n\n",
    "$$\n",
    "which can be evaluated for $x$ in the reals, complexes, or any other ring.\n",
    " More generally, if we have a function defined in Julia that does not call any\n",
    "C libraries it can be evaluated on different types. \n",
    "3. _Graph function_: The function is built by composing different basic \"kernels\" with known differentiability properties.\n",
    "We won't consider this situation in this module, though it is the model used by Python machine learning toolbox's\n",
    "like [PyTorch](https://pytorch.org) and [TensorFlow](http://tensorflow.org).\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "**Definition 1 (Dual numbers)** Dual numbers $ð”»$ are a commutative ring (over $â„$)\n",
    "generated by $1$ and $Ïµ$ such that $Ïµ^2 = 0$.\n",
    "Dual numbers are typically written as $a + b Ïµ$ where $a$ and $b$ are real.\n",
    "\n",
    "This is very much analoguous to complex numbers, which are a field generated by $1$ and ${\\rm i}$ such that\n",
    "${\\rm i}^2 = -1$. Compare multiplication of each number type:\n",
    "$$\n",
    "\\begin{align*}\n",
    "(a + b {\\rm i}) (c + d {\\rm i}) &= ac + (bc + ad) {\\rm i} + bd {\\rm i}^2 = ac -bd + (bc + ad) {\\rm i} \\\\\n",
    "(a + b Ïµ) (c + d Ïµ) &= ac + (bc + ad) Ïµ + bd Ïµ^2 = ac  + (bc + ad) Ïµ \n",
    "\\end{align*}\n",
    "$$\n",
    "And just as we view $â„ âŠ‚ â„‚$ by equating $a âˆˆ â„$ with $a + 0{\\rm i} âˆˆ â„‚$,\n",
    "we can view $â„ âŠ‚ ð”»$ by equating $a âˆˆ â„$ with $a + 0{\\rm Ïµ} âˆˆ ð”»$.\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "## 1. Differentiating polynomials\n",
    "\n",
    "Applying a polynomial to a dual number $a + b Ïµ$ tells us the derivative at $a$:\n",
    "\n",
    "**Theorem 1 (polynomials on dual numbers)** Suppose $p$ is a polynomial. Then\n",
    "$$\n",
    "p(a + b Ïµ) = p(a) + b p'(a) Ïµ\n",
    "$$\n",
    "\n",
    "**Proof**\n",
    "\n",
    "It suffices to consider $p(x) = x^n$ for $n â‰¥ 1$ as other polynomials follow from linearity. We proceed by induction:\n",
    "The case $n = 1$ is trivial. For $n > 1$ we have \n",
    "$$\n",
    "(a + b Ïµ)^n = (a + b Ïµ) (a + b Ïµ)^{n-1} = (a + b Ïµ) (a^{n-1} + (n-1) b a^{n-2} Ïµ) = a^n + b n a^{n-1} Ïµ.\n",
    "$$\n",
    "\n",
    "âˆŽ\n",
    "\n",
    "**Example 1 (differentiating polynomial)** Consider computing $p'(2)$ where\n",
    "$$\n",
    "p(x) = (x-1)(x-2) + x^2.\n",
    "$$\n",
    "We can use Dual numbers to differentiating, avoiding expanding in monomials\n",
    "or rules of differentiating:\n",
    "$$\n",
    "p(2+Ïµ) = (1+Ïµ)Ïµ + (2+Ïµ)^2 = Ïµ + 4 + 4Ïµ = 4 + \\underbrace{5}_{p'(2)}Ïµ\n",
    "$$\n",
    "\n",
    "\n",
    "\n",
    "## 2. Differentiating other functions\n",
    "\n",
    "\n",
    "We can extend real-valued differentiable functions to dual numbers in a similar manner.\n",
    "First, consider a standard function with a Taylor series (e.g. ${\\rm cos}$, ${\\rm sin}$, ${\\rm exp}$, etc.)\n",
    "$$\n",
    "f(x) = âˆ‘_{k=0}^âˆž f_k x^k\n",
    "$$\n",
    "so that $a$ is inside the radius of convergence. This leads naturally to a definition on dual numbers:\n",
    "$$\n",
    "\\begin{align*}\n",
    "f(a + b Ïµ) &= âˆ‘_{k=0}^âˆž f_k (a + b Ïµ)^k = f_0 + âˆ‘_{k=1}^âˆž f_k (a^k + k a^{k-1} b Ïµ) = âˆ‘_{k=0}^âˆž f_k a^k +  âˆ‘_{k=1}^âˆž f_k k a^{k-1} b Ïµ  \\\\\n",
    "  &= f(a) + b f'(a) Ïµ\n",
    "\\end{align*}\n",
    "$$\n",
    "More generally, given a differentiable function we can extend it to dual numbers:\n",
    "\n",
    "**Definition 2 (dual extension)** Suppose a real-valued function $f$\n",
    "is differentiable at $a$. If\n",
    "$$\n",
    "f(a + b Ïµ) = f(a) + b f'(a) Ïµ\n",
    "$$\n",
    "then we say that it is a _dual extension at_ $a$.\n",
    "\n",
    "Thus, for basic functions we have natural extensions:\n",
    "$$\n",
    "\\begin{align*}\n",
    "\\exp(a + b Ïµ) &:= \\exp(a) + b \\exp(a) Ïµ \\\\\n",
    "\\sin(a + b Ïµ) &:= \\sin(a) + b \\cos(a) Ïµ \\\\\n",
    "\\cos(a + b Ïµ) &:= \\cos(a) - b \\sin(a) Ïµ \\\\\n",
    "\\log(a + b Ïµ) &:= \\log(a) + {b \\over a} Ïµ \\\\\n",
    "\\sqrt{a+b Ïµ} &:= \\sqrt{a} + {b \\over 2 \\sqrt{a}} Ïµ \\\\\n",
    "|a + b Ïµ| &:= |a| + b\\, {\\rm sign} a\\, Ïµ\n",
    "\\end{align*}\n",
    "$$\n",
    "provided the function is differentiable at $a$. Note the last example does not have\n",
    "a convergent Taylor series (at 0) but we can still extend it where it is differentiable.\n",
    "\n",
    "Going further, we can add, multiply, and compose such functions:\n",
    "\n",
    "**Lemma 1 (product and chain rule)**\n",
    "If $f$ is a dual extension at $g(a)$ and $g$\n",
    "is a dual extension at $a$, then $q(x) := f(g(x))$ is a dual extension at $a$.\n",
    "If $f$ and $g$ are dual extensions at $a$ then \n",
    "$r(x) := f(x) g(x)$ is also dual extensions at $a$. In other words:\n",
    "$$\n",
    "\\begin{align*}\n",
    "q(a+b Ïµ) &= q(a) + b q'(a) Ïµ \\\\\n",
    "r(a+b Ïµ) &= r(a) + b r'(a) Ïµ\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "**Proof**\n",
    "For $q$ it follows immediately:\n",
    "$$\n",
    "\\begin{align*}\n",
    "q(a + b Ïµ) &= f(g(a + b Ïµ)) = f(g(a) + b g'(a) Ïµ) \\\\\n",
    "&= f(g(a)) + b g'(a) f'(g(a))Ïµ = q(a) + b q'(a) Ïµ.\n",
    "\\end{align*}\n",
    "$$\n",
    "For $r$ we have\n",
    "$$\n",
    "\\begin{align*}\n",
    "r(a + b Ïµ) &= f(a+b Ïµ )g(a+b Ïµ )= (f(a) + b f'(a) Ïµ)(g(a) + b g'(a) Ïµ) \\\\\n",
    "&= f(a)g(a) + b (f'(a)g(a) + f(a)g'(a)) Ïµ = r(a) +b r'(a) Ïµ.\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "âˆŽ\n",
    "\n",
    "A simple corollary is that any function defined in terms of addition, multiplication, composition, etc.\n",
    "of functions that are dual with differentiation will be differentiable via dual numbers.\n",
    "\n",
    "**Example 2 (differentiating non-polynomial)**\n",
    "\n",
    "Consider $f(x) =  \\exp(x^2 + {\\rm e}^{x})$ by evaluating on the duals:\n",
    "$$\n",
    "f(1 + Ïµ) = \\exp(1 + 2Ïµ + {\\rm e} + {\\rm e} Ïµ) =  \\exp(1 + {\\rm e}) + \\exp(1 + {\\rm e}) (2 + {\\rm e}) Ïµ\n",
    "$$\n",
    "and therefore we deduce that\n",
    "$$\n",
    "f'(1) = \\exp(1 + {\\rm e}) (2 + {\\rm e}).\n",
    "$$\n",
    "\n",
    "\n",
    "## 3. Implementation as a special type\n",
    "\n",
    "\n",
    "We now consider a simple implementation of dual numbers that works on general polynomials:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:04.121824Z",
     "iopub.status.busy": "2023-01-26T20:43:03.514051Z",
     "iopub.status.idle": "2023-01-26T20:43:04.864095Z",
     "shell.execute_reply": "2023-01-26T20:43:04.863535Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "exp (generic function with 15 methods)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Dual(a,b) represents a + b*Ïµ\n",
    "struct Dual{T}\n",
    "    a::T\n",
    "    b::T\n",
    "end\n",
    "\n",
    "# Dual(a) represents a + 0*Ïµ\n",
    "Dual(a::Real) = Dual(a, zero(a)) # for real numbers we use a + 0Ïµ\n",
    "\n",
    "# Allow for a + b*Ïµ syntax\n",
    "const Ïµ = Dual(0, 1)\n",
    "\n",
    "import Base: +, *, -, /, ^, zero, exp\n",
    "\n",
    "# support polynomials like 1 + x, x - 1, 2x or x*2 by reducing to Dual\n",
    "+(x::Real, y::Dual) = Dual(x) + y\n",
    "+(x::Dual, y::Real) = x + Dual(y)\n",
    "-(x::Real, y::Dual) = Dual(x) - y\n",
    "-(x::Dual, y::Real) = x - Dual(y)\n",
    "*(x::Real, y::Dual) = Dual(x) * y\n",
    "*(x::Dual, y::Real) = x * Dual(y)\n",
    "\n",
    "# support x/2 (but not yet division of duals)\n",
    "/(x::Dual, k::Real) = Dual(x.a/k, x.b/k)\n",
    "\n",
    "# a simple recursive function to support x^2, x^3, etc.\n",
    "function ^(x::Dual, k::Integer)\n",
    "    if k < 0\n",
    "        error(\"Not implemented\")\n",
    "    elseif k == 1\n",
    "        x\n",
    "    else\n",
    "        x^(k-1) * x\n",
    "    end\n",
    "end\n",
    "\n",
    "# Algebraic operationds for duals\n",
    "-(x::Dual) = Dual(-x.a, -x.b)\n",
    "+(x::Dual, y::Dual) = Dual(x.a + y.a, x.b + y.b)\n",
    "-(x::Dual, y::Dual) = Dual(x.a - y.a, x.b - y.b)\n",
    "*(x::Dual, y::Dual) = Dual(x.a*y.a, x.a*y.b + x.b*y.a)\n",
    "\n",
    "exp(x::Dual) = Dual(exp(x.a), exp(x.a) * x.b)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We can also try it on the two polynomials as above:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:04.866203Z",
     "iopub.status.busy": "2023-01-26T20:43:04.865643Z",
     "iopub.status.idle": "2023-01-26T20:43:05.254681Z",
     "shell.execute_reply": "2023-01-26T20:43:05.254224Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(1, 0.3333333333333333)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "f = x -> 1 + x + x^2\n",
    "g = x -> 1 + x/3 + x^2\n",
    "f(Ïµ).b, g(Ïµ).b"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The first example exactly computes the derivative, and the\n",
    "second example is exact up to the last bit rounding!\n",
    "It also works for higher order polynomials:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:05.257090Z",
     "iopub.status.busy": "2023-01-26T20:43:05.256426Z",
     "iopub.status.idle": "2023-01-26T20:43:05.488735Z",
     "shell.execute_reply": "2023-01-26T20:43:05.488295Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "8.881784197001252e-16"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "f = x -> 1 + 1.3x + 2.1x^2 + 3.1x^3\n",
    "f(0.5 + Ïµ).b - 5.725"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "It is indeed \"accurate to (roughly) 16-digits\", the best we can hope for \n",
    "using floating point.\n",
    "\n",
    "We can use this in \"algorithms\" as well as simple polynomials.\n",
    "Consider the polynomial $1 + â€¦ + x^n$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:05.490969Z",
     "iopub.status.busy": "2023-01-26T20:43:05.490394Z",
     "iopub.status.idle": "2023-01-26T20:43:05.507994Z",
     "shell.execute_reply": "2023-01-26T20:43:05.507575Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1.2345678999999998"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function s(n, x)\n",
    "    ret = 1 + x # first two terms\n",
    "    for k = 2:n\n",
    "        ret += x^k\n",
    "    end\n",
    "    ret\n",
    "end\n",
    "s(10, 0.1 + Ïµ).b"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This matches exactly the \"true\" (up to rounding) derivative:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:05.510184Z",
     "iopub.status.busy": "2023-01-26T20:43:05.509645Z",
     "iopub.status.idle": "2023-01-26T20:43:05.621365Z",
     "shell.execute_reply": "2023-01-26T20:43:05.620938Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1.2345678999999998"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "sum((1:10) .* 0.1 .^(0:9))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Finally, we can try the more complicated example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "execution": {
     "iopub.execute_input": "2023-01-26T20:43:05.623586Z",
     "iopub.status.busy": "2023-01-26T20:43:05.623040Z",
     "iopub.status.idle": "2023-01-26T20:43:06.270144Z",
     "shell.execute_reply": "2023-01-26T20:43:06.269724Z"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Dual{Float64}(41.193555674716116, 194.362805189629)"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "f = x -> exp(x^2 + exp(x))\n",
    "f(1 + Ïµ)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "What makes dual numbers so effective is that, unlike divided differences, they are not\n",
    "prone to disasterous growth due to round-off errors."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.3-pre",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
